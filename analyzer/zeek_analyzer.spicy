module Zeek_zeek_spicy_openvpn;

import zeek_spicy_openvpn;
import zeek;

function initialize_connection(inout ctx: zeek_spicy_openvpn::Context) {
	if ( ! ctx.initialized ) {
		ctx.initialized = True;
		zeek::protocol_begin("SSL");
	}
}

on zeek_spicy_openvpn::ControlMessage::%done {
	if ((self.opcode == zeek_spicy_openvpn::Opcode::P_CONTROL_HARD_RESET_CLIENT_V1 ||
		 self.opcode == zeek_spicy_openvpn::Opcode::P_CONTROL_HARD_RESET_SERVER_V2 ||
		 self.opcode == zeek_spicy_openvpn::Opcode::P_CONTROL_SOFT_RESET_V1 ||
		 self.opcode == zeek_spicy_openvpn::Opcode::P_CONTROL_HARD_RESET_CLIENT_V2 ||
		 self.opcode == zeek_spicy_openvpn::Opcode::P_CONTROL_HARD_RESET_SERVER_V2)
	     && |self.ssl_data| > 0)
		zeek::reject_protocol("should not have ssl_data.");
	zeek::confirm_protocol();
}

on zeek_spicy_openvpn::OpenVPNRecord::%error {
	zeek::reject_protocol("error while parsing OpenVPN record");
}

on zeek_spicy_openvpn::OpenVPNRecordTCP::%error {
	zeek::reject_protocol("error while parsing OpenVPN record");
}

on zeek_spicy_openvpn::ControlMessage::%done {
    zeek::protocol_data_in(zeek::is_orig(), self.ssl_data);
}

on zeek_spicy_openvpn::OpenVPNRecords::%init {
    initialize_connection(self.context());
}

on zeek_spicy_openvpn::OpenVPNRecordsHMACMD5::%init {
    initialize_connection(self.context());
}

on zeek_spicy_openvpn::OpenVPNRecordsHMACSHA1::%init {
    initialize_connection(self.context());
}

on zeek_spicy_openvpn::OpenVPNRecordsHMACSHA256::%init {
    initialize_connection(self.context());
}

on zeek_spicy_openvpn::OpenVPNRecordsHMACSHA512::%init {
    initialize_connection(self.context());
}

on zeek_spicy_openvpn::OpenVPNRecordsTCP::%init {
    initialize_connection(self.context());
}

on zeek_spicy_openvpn::OpenVPNRecordsTCPHMACMD5::%init {
    initialize_connection(self.context());
}

on zeek_spicy_openvpn::OpenVPNRecordsTCPHMACSHA1::%init {
    initialize_connection(self.context());
}

on zeek_spicy_openvpn::OpenVPNRecordsTCPHMACSHA256::%init {
    initialize_connection(self.context());
}

on zeek_spicy_openvpn::OpenVPNRecordsTCPHMACSHA512::%init {
    initialize_connection(self.context());
}

public function create_controlmsg(msg: zeek_spicy_openvpn::ControlMessage):
    tuple<
        opcode: uint8,
        key_id: uint8,
        session_id: optional<bytes>,
        packet_id_ack_array: optional<vector<uint32>>,
        remote_session_id: optional<bytes>,
        packet_id: optional<uint32>,
        data_len: uint64> {
	local rs: optional<bytes>;
	local acks: optional<vector<uint32>>;
	if (msg.packet_id_array_len > 0)
		{
		rs = msg.remote_session_id;
		acks = msg.packet_id_array;
		}
    return (
        cast<uint8>(msg.opcode),
        msg.key_id,
        msg.session_id,
        acks,
        rs,
        msg.packet_id,
        |msg.ssl_data|
    );
}

public function create_ackmsg(msg: zeek_spicy_openvpn::AckMessage):
    tuple<
        opcode: uint8,
        key_id: uint8,
        session_id: optional<bytes>,
        packet_id_ack_array: optional<vector<uint32>>,
        remote_session_id: optional<bytes>> {
    return (
        cast<uint8>(msg.opcode),
        msg.key_id,
        msg.session_id,
        msg.packet_id_array,
        msg.remote_session_id
    );
}

public function create_datamsg(msg: zeek_spicy_openvpn::DataMessage):
    tuple<
        opcode: uint8,
        key_id: uint8,
        data_len: uint64,
        peer_id: optional<bytes>> {
	local mypeerid: optional<bytes>;
	if (msg.opcode == zeek_spicy_openvpn::Opcode::P_DATA_V2)
		mypeerid = msg.peer_id;
    return (
        cast<uint8>(msg.opcode),
        msg.key_id,
        |msg.payload|,
        mypeerid
    );
}
