module zeek_spicy_openvpn;

import spicy;
import zeek;

%byte-order = spicy::ByteOrder::Big;

type Context = tuple<initialized: bool>;

function initialize_connection(inout ctx: Context) {
	if ( ! ctx.initialized ) {
		ctx.initialized = True;
		zeek::protocol_begin("SSL");
	}
}

type Opcode = enum {
	P_CONTROL_HARD_RESET_CLIENT_V1	= 0x01,
	P_CONTROL_HARD_RESET_SERVER_V1	= 0x02,
	P_CONTROL_SOFT_RESET_V1		= 0x03,
	P_CONTROL_V1			= 0x04,
	P_ACK_V1			= 0x05,
	P_DATA_V1			= 0x06,
	P_CONTROL_HARD_RESET_CLIENT_V2	= 0x07,
	P_CONTROL_HARD_RESET_SERVER_V2	= 0x08,
	P_DATA_V2			= 0x09
};

public type OpenVPNRecords = unit {
	%context = Context;

	records: OpenVPNRecord(0)[];

	on %init {
		initialize_connection(self.context());
	}
};

public type OpenVPNRecordsHMACMD5 = unit {
	%context = Context;

	records: OpenVPNRecord(16)[];

	on %init {
		initialize_connection(self.context());
	}
};

public type OpenVPNRecordsHMACSHA1 = unit { ##
	%context = Context;

	records: OpenVPNRecord(20)[]; # 160 bit

	on %init {
		initialize_connection(self.context());
	}
};

public type OpenVPNRecordsHMACSHA256 = unit {
	%context = Context;

	records: OpenVPNRecord(32)[];

	on %init {
		initialize_connection(self.context());
	}
};

public type OpenVPNRecordsHMACSHA512 = unit {
	%context = Context;

	records: OpenVPNRecord(64)[];

	on %init {
		initialize_connection(self.context());
	}
};

public type OpenVPNRecord = unit(hmac_size: uint32) {
	message_type: bitfield(8) {
		opcode: 3..7 &convert=Opcode($$);
		key_id: 0..2;
	};

	switch ( self.message_type.opcode ) {
		Opcode::P_CONTROL_HARD_RESET_CLIENT_V1 -> control_hard_reset_client_v1: ControlMessage(self.message_type.opcode, self.message_type.key_id, hmac_size);
		Opcode::P_CONTROL_HARD_RESET_SERVER_V1 -> control_hard_reset_server_v1: ControlMessage(self.message_type.opcode, self.message_type.key_id, hmac_size);
		Opcode::P_CONTROL_SOFT_RESET_V1 -> control_soft_reset_v1: ControlMessage(self.message_type.opcode, self.message_type.key_id, hmac_size);
		Opcode::P_CONTROL_V1 -> control_v1: ControlMessage(self.message_type.opcode, self.message_type.key_id, hmac_size);
		Opcode::P_ACK_V1 -> ack_v1: AckMessage(self.message_type.opcode, self.message_type.key_id, hmac_size);
		Opcode::P_DATA_V1 -> data_v1: DataMessage(self.message_type.opcode, self.message_type.key_id, False);
		Opcode::P_CONTROL_HARD_RESET_SERVER_V2 -> control_hard_reset_server_v2: ControlMessage(self.message_type.opcode, self.message_type.key_id, hmac_size);
		Opcode::P_CONTROL_HARD_RESET_CLIENT_V2 -> control_hard_reset_client_v2: ControlMessage(self.message_type.opcode, self.message_type.key_id, hmac_size);
		Opcode::P_DATA_V2 -> data_v2: DataMessage(self.message_type.opcode, self.message_type.key_id, True);
	};
};

public type OpenVPNRecordsTCP = unit {
	%context = Context;

	records: OpenVPNRecordTCP(0)[];

	on %init {
		initialize_connection(self.context());
	}
};

public type OpenVPNRecordsTCPHMACMD5 = unit {
	%context = Context;

	records: OpenVPNRecordTCP(16)[];

	on %init {
		initialize_connection(self.context());
	}
};

public type OpenVPNRecordsTCPHMACSHA1 = unit {
	%context = Context;

	records: OpenVPNRecordTCP(20)[];

	on %init {
		initialize_connection(self.context());
	}
};

public type OpenVPNRecordsTCPHMACSHA256 = unit {
	%context = Context;

	records: OpenVPNRecordTCP(32)[];

	on %init {
		initialize_connection(self.context());
	}
};

public type OpenVPNRecordsTCPHMACSHA512 = unit {
	%context = Context;

	records: OpenVPNRecordTCP(64)[];

	on %init {
		initialize_connection(self.context());
	}
};

public type OpenVPNRecordTCP = unit(hmac_size: uint32) {
	packet_length: uint16;

	record: OpenVPNRecord(hmac_size) &size=self.packet_length;
};

type HMACInfo = unit(hmac_size: uint32) {
	hmac : bytes &size=hmac_size;
	packet_id: uint32;
	net_time: bytes &size=4;
};

public type ControlMessage = unit(opcode: Opcode, key_id: uint8, hmac_size: uint32) {
	var opcode: Opcode = opcode;
	var key_id: uint8 = key_id;
	session_id: bytes &size=8;
	hmac: HMACInfo(hmac_size) if (hmac_size > 0);
	packet_id_array_len: uint8;
	packet_id_array: uint32[self.packet_id_array_len];
	remote_session_id: bytes &size=8 if (self.packet_id_array_len > 0);
	packet_id: uint32;
	ssl_data: bytes &eod;

	on %done {
		zeek::protocol_data_in(zeek::is_orig(), self.ssl_data);
	}
};

type AckMessage = unit(opcode: Opcode, key_id: uint8, hmac_size: uint32) {
	var opcode: Opcode = opcode;
	var key_id: uint8 = key_id;
	session_id: bytes &size=8;
	hmac: HMACInfo(hmac_size) if (hmac_size > 0);
	packet_id_array_len : uint8;
	packet_id_array: uint32[self.packet_id_array_len];
	remote_session_id: bytes &size=8;
};

type DataMessage = unit(opcode: Opcode, key_id: uint8, is_v2: bool) {
	var opcode: Opcode = opcode;
	var key_id: uint8 = key_id;
	peer_id: bytes &size=3 if (is_v2);
	payload: bytes &eod;
};
